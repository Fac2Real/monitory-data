plugins {
    id 'java'
    id 'application'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
}

group = 'org.example'
version = '1.0-SNAPSHOT'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(11)
    }
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

application {
    mainClass = 'com.monitory.data.FlinkSourceApplication'
}

shadowJar {
    archiveClassifier.set('')
    mergeServiceFiles()
    manifest {
        attributes 'Main-Class': 'com.monitory.data.FlinkSourceApplication'
    }
    zip64 true
}

tasks.named('startScripts') {
    dependsOn shadowJar
}

tasks.named('jar') {
    enabled = false
}

tasks.withType(Tar) {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}
tasks.withType(Zip) {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

tasks.withType(Test) {
    maxHeapSize = "4g"
}
tasks.withType(JavaCompile) {
    options.fork = true
    options.forkOptions.jvmArgs = ['-Xmx4g']
}


distributions {
    main {
        contents {
            into('lib') {
                from shadowJar
            }
        }
    }
}

// 기존 jar 대신 shadowJar를 빌드
tasks.build.dependsOn tasks.shadowJar

repositories {
    mavenCentral()
}

dependencies {
    testImplementation platform('org.junit:junit-bom:5.10.0')
    testImplementation 'org.junit.jupiter:junit-jupiter'
    compileOnly 'org.projectlombok:lombok:1.18.30'
    annotationProcessor 'org.projectlombok:lombok:1.18.30'

    implementation 'ch.qos.logback:logback-classic:1.2.6'

    // flink 1.15.4 with Scala 2.12
    implementation "org.apache.flink:flink-streaming-java:1.15.4"
    implementation "org.apache.flink:flink-connector-kafka:1.15.4"
    implementation "org.apache.flink:flink-connector-files:1.15.4"
    implementation "org.apache.flink:flink-connector-base:1.15.4"
    implementation "org.apache.flink:flink-clients:1.15.4"
    implementation "org.apache.flink:flink-runtime-web:1.15.4"
    implementation "org.apache.flink:flink-s3-fs-hadoop:1.15.4"

    // MQTT 메세지 송수신을 위한 의존성 추가
    implementation 'org.eclipse.paho:org.eclipse.paho.client.mqttv3:1.2.5'
    implementation 'org.bouncycastle:bcprov-jdk15on:1.70'
    implementation 'org.bouncycastle:bcpkix-jdk15on:1.70'

    // JSON 파싱
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.15.2'

    // InfluxDB 클라이언트
    implementation "com.influxdb:influxdb-client-java:7.2.0"

    // prometheus
    implementation "org.apache.flink:flink-metrics-prometheus:1.15.4"
    
    // Kinesis
    implementation "org.apache.flink:flink-connector-kinesis:1.15.4"

    // hadoop-aws
    implementation("org.apache.hadoop:hadoop-aws:3.4.1")
    implementation 'com.amazonaws:aws-java-sdk-bundle:1.12.783'
}
test {
    useJUnitPlatform()
}

